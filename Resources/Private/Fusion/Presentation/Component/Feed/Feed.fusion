prototype(RobertLemke.Plugin.Blog:Presentation.Component.Feed) < prototype(Neos.Fusion:Component) {

    title = ''
    siteLink = ''
    atomLink = ''
    description = ''
    language = ''
    publishDate = null
    posts = ${[]}

    @private {
        channelInformation = Neos.Fusion:Join {
            title = Neos.Fusion:Tag {
                tagName = 'title'
                content = ${props.title}
            }
            // cannot use Neos.Fusion:Tag â€“ "link" is handled as self-closing and will never show content
            siteLink = ${'<link>' + props.siteLink + '</link>'}
            atomLink = Neos.Fusion:Tag {
                tagName = 'atom-link'
                attributes.href = ${props.atomLink}
                attributes.rel = 'self'
                selfClosingTag = true
            }
            description = Neos.Fusion:Tag {
                tagName = 'description'
                content = ${props.description}
            }
            language = Neos.Fusion:Tag {
                tagName = 'language'
                content = ${props.language}
            }
            lastBuildDate = Neos.Fusion:Tag {
                tagName = 'lastBuildDate'
                content = ${Date.format(props.publishDate, 'r')}
            }
        }
        postItems = Neos.Fusion:Loop {
            items = ${props.posts}
            itemName = 'post'
            itemRenderer = RobertLemke.Plugin.Blog:Presentation.Component.Feed.FeedItem {
                title = ${post.properties.title}
                description = ${RobertLemke.Plugin.Blog.Teaser.getCroppedTeaser(post)}
                link = Neos.Neos:NodeUri {
                    node = ${post}
                    format = 'html'
                    absolute = true
                }
                publishDate = ${post.properties.datePublished}
                identifier = ${post.aggregateId}
            }
        }
    }

    renderer = afx`
        <Neos.Fusion:Fragment>
            <rss version="2.0" xmlns:atom="http://www.w3.org/2005/Atom">
                <channel>
                    {private.channelInformation}
                    {private.postItems}
                </channel>
            </rss>
        </Neos.Fusion:Fragment>
    `
}
