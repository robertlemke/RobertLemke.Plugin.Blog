prototype(RobertLemke.Plugin.Blog:Presentation.Component.Feed.FeedItem) < prototype(Neos.Fusion:Component) {
    title = ''
    link = ''
    description = ''
    publishDate = null
    identifier = ''

    renderer = Neos.Fusion:Tag {
        tagName = 'item'
        content = Neos.Fusion:Join {
            title = Neos.Fusion:Tag {
                tagName = 'title'
                content = ${String.stripTags(props.title)}
            }
            // cannot use Neos.Fusion:Tag â€“ "link" is handled as self-closing and will never show content
            link = ${'<link>' + props.link + '</link>'}
            description = Neos.Fusion:Tag {
                tagName = 'description'
                content = ${String.htmlSpecialChars(String.stripTags(props.description))}
            }
            pubDate = Neos.Fusion:Tag {
                tagName = 'pubDate'
                content = ${Date.format(props.publishDate, 'r')}
            }
            guid = Neos.Fusion:Tag {
                tagName = 'guid'
                attributes = Neos.Fusion:DataStructure {
                    isPermaLink = 'false'
                    @if.hasIdentifier = ${!String.isBlank(props.identifier)}
                }
                content = ${props.identifier}
            }
        }
    }
}
